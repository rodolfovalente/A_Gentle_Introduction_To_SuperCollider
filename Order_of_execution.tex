\section{Ordem de Execução}
\label{sec:order-of-execution}

Quando discutimos Canais de Áudio na seção \ref{sec:audiobus} sinalizamos a importância da ordem de execução. O código abaixo é uma versão expandida do exemplo de ruído filtrado daquela seção. A discussão que se segue explicará o conceito básico de ordem de execução, demonstrando porque ele é importante.


\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// Crie um canal (“bus”) de áudio
~busEfeitos = Bus.audio(s, 1);
~busMaster = Bus.audio(s, 1);
// Criar SynthDefs
(
SynthDef(“ruido”, {Out.ar(~busEfeitos, WhiteNoise.ar(0.5))}).add;
SynthDef(“filtro", {Out.ar(~busMaster, BPF.ar(in: In.ar(~busEfeitos), freq: MouseY.kr(1000, 5000), rq: 0.1))}).add;
SynthDef(“saidaMaster", {arg amp = 1; Out.ar(0, In.ar(~busMaster) * Lag.kr(amp, 1))}).add;
)
// Abra a janela Node Tree (“Árvore de Nós”):
s.plotTree;
// Tocar sintetizadores (observe a Node Tree)
m = Synth(“saidaMaster”);
f = Synth("filter");
n = Synth("ruido");
// Volume master
m.set(\amp, 0.1);
\end{lstlisting}

Primeiro, dois canais de áudio são atribuídos às variáveis \texttt{$\sim$busEfeitos} and \texttt{$\sim$busMaster}.

Segundo, três \texttt{SynthDef}s são criados:
\begin{itemize}
\item \texttt{“ruido”} é uma fonte de ruído que manda ruído branco para o canal de efeitos;
\item \texttt{"filtro”} é um filtro passa-banda que recebe uma entrada do canal de efeitos e envia o som processado para o canal master;
\item \texttt{“saidaMaster"} recebe sinal do canal master e aplica um controle simples de volume, enviando o som final com o volume ajustado para os alto-falantes.
\end{itemize}

Observe a Node Tree enquanto você roda os sintetizadores na ordem.

\begin{figure}[h]
\centerline{
	\includegraphics[scale=0.5]{fig-node-tree.png}}
\caption{Nós de sintetizador na janela da Node Tree}
\label{fig:node-tree}
\end{figure}

Nós de sintetizador na janela da Node Tree correm \emph{de cima para baixo}. Os sintetizadores mais recentes são adicionados ao topo por definição. Na figura \ref{fig:node-tree}, você pode ver que  \texttt{“ruído”} está no topo, \texttt{“filtro”} vem em segundo e \texttt{“saidaMaster"} vem por último. Esta é a ordem certa que queremos: lendo de cima para baixo, a fonte de ruído flui para o filtro e o resultado do filtro flui para o canal master. Se você tentar rodar o exemplo de novo, mas rodando as linhas \texttt{m}, \texttt{f} e \texttt{n} em ordem inversa, você não ouvirá nada, porque os sinais estão sendo calculados na ordem errada.

Rodas as linhas certas na ordem certa é bom, mas pode se tornar capciosa se seu código se tornar mais complexo. Para facilitar este trabalho, o SuperCollider permite que você defina explicitamente onde posicionar os sintetizadores na Árvore de Nós. Para isso, usamos os argumentos \texttt{target} (“destino”) e \texttt{addAction} (“adicionar ação”).

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
n = Synth(“ruido”, addAction: 'addToHead');
m = Synth(“saidaMaster", addAction: 'addToTail');
f = Synth(“filtro”, target: n, addAction: 'addAfter');
\end{lstlisting}
Agora não importa em que ordem você executar as linhas acima, você pode ter certeza que os nós cairão nos lugares certos. Ao sintetizador \texttt{"noise"} pede-se explicitamente que seja adicionado ao início (“Head”, cabeça) da Node Tree; \texttt{“saidaMaster"} é adicionada ao final (“Tail”, cauda); e o \texttt{filtro} é explicitamente adicionado logo depois (“After”) do destino (“target”) \texttt{n} (o sintetizador de ruído).

\subsection{Grupos}

Quando você começa a ter um monte de sintetizadores---alguns para fontes sonoras, outros para efeitos ou o que você precisar---pode ser uma boa organizá-los em grupos. Eis um exemplo básico:

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// Fique observando tudo que acontece na Node Tree
s.plotTree;

// Criar alguns buses
~busReverb = Bus.audio(s, 2);
~busMaster = Bus.audio(s, 2);

// Definir grupos
(
~fontes = Group.new;
~efeitos = Group.new(~fontes, \addAfter);
~master = Group.new(~efeitos, \addAfter);
)

// Rodar todos os sintetizadores de uma vez
(
// Uma fonte sonora
{
  Out.ar(~busReverb, SinOsc.ar([800, 890])*LFPulse.ar(2)*0.1)
}.play(target: ~fontes);

// Outra fonte sonora
{
  Out.ar(~busReverb, WhiteNoise.ar(LFPulse.ar(2, 1/2, width: 0.05)*0.1))
}.play(target: ~fontes);

// Um pouco de reverb
{
  Out.ar(~busMaster, FreeVerb.ar(In.ar(~busReverb, 2), mix: 0.5, room: 0.9))
}.play(target: ~efeitos);

// Um controle bobo de volume com o mouse
{
  Out.ar(0, In.ar(~busMaster, 2) * MouseY.kr(0, 1))
}.play(target: ~master);
)
\end{lstlisting}

Para mais informações sobre ordem de execução, olhe os arquivos de Ajuda “Synth”, “Order of Execution” e “Group”.
